#!/usr/bin/perl
# /*
#  * if - a plugin for munin-node-win32-cygwin
#  *
#  * Copyright (c) 2015 John Vinopal
#  * Licensed under the MIT license.
#  */
use strict;
use warnings;
#
# Munin plugin to monitor interface usage.
#
# Parameters:
#       config
#       autoconf
#
# Requirements:
#       none
#

# Windows netstat.  Assuming installed on C: drive.
my $NETSTAT_COMMAND = '/cygdrive/c/Windows/system32/netstat';
my $NETSTAT_ARGS    = '-e';

# Munin Configuration
if (@ARGV) {
    if ( $ARGV[0] eq 'autoconf' ) {
        if ( -e $NETSTAT_COMMAND ) {
            print "yes\n";
        }
        else {
            print "no (netstat not found, non-C: install?)\n";
        }
        exit 0;
    }
    elsif ( $ARGV[0] eq 'config' ) {
        print "graph_category network\n";
        print "graph_title Network traffic\n";
        print "graph_args --base 1000\n";
        print "graph_vlabel bits per \${graph_period} in (-) / out (+)\n";
        print "graph_info This graph shows the traffic of the network interface. Please note that the traffic is shown in bits per second, not bytes.\n";
        print "graph_order rbytes sbytes\n";

        print "rbytes.label received\n";
        print "rbytes.type DERIVE\n";
        print "rbytes.graph no\n";
        print "rbytes.cdef rbytes,8,*\n";
        print "rbytes.min 0\n";
        print "sbytes.label bps\n";
        print "sbytes.type DERIVE\n";
        print "sbytes.negative rbytes\n";
        print "sbytes.cdef sbytes,8,*\n";
        print "sbytes.min 0\n";
        print "sbytes.info Traffic sent (+) and received (-) on the network interface.\n";
        exit 0;
    }
    else {
        print "Unknown arg $ARGV[0]\n";
        exit 1;
    }
}

# Munin Data
my %netstat = fetchNetstatData();
print "rbytes.value $netstat{rbytes}\n";
print "sbytes.value $netstat{sbytes}\n";
exit 0;

# Returns sent and recv data in bytes.
sub fetchNetstatData {
    my $data = `$NETSTAT_COMMAND $NETSTAT_ARGS`;

    #$ netstat -e
    #Interface Statistics
    #
    #                           Received            Sent
    #
    #Bytes                    2585549224      1974684958
    #Unicast packets            76309380        11487700
    #Non-unicast packets         1108826          600790
    #Discards                       3972            3972
    #Errors                            0               0
    #Unknown protocols                 0

    my ( $rbytes, $sbytes ) = $data =~ /^Bytes\s+(\d+)\s+(\d+)/m;
    $rbytes = 'U' if ( not defined $rbytes );
    $sbytes = 'U' if ( not defined $sbytes );
    my %netstat = ( "rbytes" => $rbytes, "sbytes" => $sbytes );
    return %netstat;
}
