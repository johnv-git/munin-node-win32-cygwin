#!/usr/bin/perl
# /*
#  * netstat - a plugin for munin-node-win32-cygwin
#  *
#  * Copyright (c) 2015 John Vinopal
#  * Licensed under the MIT license.
#  */
use strict;
use warnings;
#
# Munin plugin to connection statistics.
#
# Parameters:
#       config
#       autoconf
#
# Requirements:
#	Cygwin, perl
#

# Windows netstat.  Assuming installed on C: drive.
my $NETSTAT_COMMAND = '/cygdrive/c/Windows/system32/netstat';
my $NETSTAT_OPTS    = '-s';

# Define Fields and Output Data
my @graph_order  = qw(active passive failed current);
my %graph_fields = (
    "active" => {
        netstat_field => "Active Opens",
        label         => "active",
        info => "The number of active TCP openings per \${graph_period}",
    },
    "passive" => {
        netstat_field => "Passive Opens",
        label         => "passive",
        info => "The number of passive TCP openings per \${graph_period}",
    },
    "failed" => {
        netstat_field => "Failed Connection Attempts",
        label         => "failed",
        info =>
          "The number of failed TCP connection attempts per \${graph_period}",
    },
    "current" => {
        netstat_field => "Current Connections",
        label         => "established",
        info          => "The number of currently open connections",
    },
);

# Munin Configuration
if (@ARGV) {
    if ( $ARGV[0] eq 'autoconf' ) {
        if ( -e $NETSTAT_COMMAND ) {
            print "yes\n";
        }
        else {
            print "no (netstat not found, non-C: install?)\n";
        }
    }
    elsif ( $ARGV[0] eq 'config' ) {
        print "graph_category network\n";
        print "graph_title Netstat\n";
        print "graph_args --base 1000 --logarithmic\n";
        print "graph_vlabel active connections per \${graph_period}\n";
        print "graph_info This graph shows the TCP activity of all the network interfaces combined.\n";
        print "graph_order @graph_order\n";

        foreach my $key ( keys %graph_fields ) {
            print "$key.label $graph_fields{$key}->{label}\n";
            print "$key.info $graph_fields{$key}->{info}\n";
            print "$key.type DERIVE\n";
        }
        exit 0;
    }
    else {
        print "Unknown arg $ARGV[0]\n";
        exit 1;
    }
}

# Munin Data
my %netstat = fetchNetstatData();
foreach my $key ( keys %graph_fields ) {
    print "$key.value $netstat{$key}\n";
}
exit 0;

# Returns hash of "active" => 12791
sub fetchNetstatData {
    my $data = `$NETSTAT_COMMAND $NETSTAT_OPTS`;

    #$ netstat -s
    #TCP Statistics for IPv4
    #
    #  Active Opens                        = 12791
    #  Passive Opens                       = 9213
    #  Failed Connection Attempts          = 88
    #  Reset Connections                   = 1466
    #  Current Connections                 = 8
    #  Segments Received                   = 28805700
    #  Segments Sent                       = 41959377
    #  Segments Retransmitted              = 10321

    my %netstat = ();
    foreach my $key ( keys %graph_fields ) {
        my $netstat_field = $graph_fields{$key}->{netstat_field};
        while ( $data =~ /^  $netstat_field\s+=\s+(\d+)/mg ) {
            $netstat{$key} += $1;
        }
        $netstat{$key} = "U" if ( not defined $netstat{$key} );
    }
    return %netstat;
}
