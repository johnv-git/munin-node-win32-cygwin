#!/usr/bin/perl
# /*
#  * if_packets - a plugin for munin-node-win32-cygwin
#  *
#  * Copyright (c) 2015 John Vinopal
#  * Licensed under the MIT license.
#  */
use strict;
use warnings;
#
# Munin plugin to monitor interface packets per second.
#
# Parameters:
#       config
#       autoconf
#
# Requirements:
#	Cygwin
#

# Windows netstat.  Assuming installed on C: drive.
my $NETSTAT_COMMAND = '/cygdrive/c/Windows/system32/netstat';
my $NETSTAT_ARGS    = '-e';

# Munin Configuration
if (@ARGV) {
    if ( $ARGV[0] eq 'autoconf' ) {
        if ( -e $NETSTAT_COMMAND ) {
            print "yes\n";
        }
        else {
            print "no (netstat not found, non-C: install?)\n";
        }
        exit 0;
    }
    elsif ( $ARGV[0] eq 'config' ) {
        print "graph_category network\n";
        print "graph_title Network traffic\n";
        print "graph_args --base 1000\n";
        print "graph_vlabel packets per \${graph_period} in (-) / out (+)\n";
        print "graph_info This graph shows the traffic of the network interface. Please note that the traffic is shown in packets per second.\n";
        print "graph_order rpackets spackets\n";

        print "rpackets.label received\n";
        print "rpackets.type DERIVE\n";
        print "rpackets.graph no\n";
        print "rpackets.min 0\n";
        print "spackets.label pps\n";
        print "spackets.type DERIVE\n";
        print "spackets.negative rpackets\n";
        print "spackets.min 0\n";
        print "spackets.info Traffic sent (+) and received (-) on the network interface.\n";
        exit 0;
    }
    else {
        print "Unknown arg $ARGV[0]\n";
        exit 1;
    }
}

# Munin Data
my %netstat = fetchNetstatData();
print "rpackets.value $netstat{rpackets}\n";
print "spackets.value $netstat{spackets}\n";
exit 0;

# Returns sent and recv data in packets.
sub fetchNetstatData {
    my $data = `$NETSTAT_COMMAND $NETSTAT_ARGS`;

    #$ netstat -e
    #Interface Statistics
    #
    #                           Received            Sent
    #
    #Bytes                    2585549224      1974684958
    #Unicast packets            76309380        11487700
    #Non-unicast packets         1108826          600790
    #Discards                       3972            3972
    #Errors                            0               0
    #Unknown protocols                 0

    my ( $recv, $send );
    while ( $data =~ /^[-\w]+ packets\s+(\d+)\s+(\d+)/mg ) {
        $recv += $1;
        $send += $2;
    }
    $recv = 'U' if ( not defined $recv );
    $send = 'U' if ( not defined $send );
    my %netstat = ( "rpackets" => $recv, "spackets" => $send );
    return %netstat;
}
