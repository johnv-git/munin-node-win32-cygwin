#!/usr/bin/perl
# /*
#  * disk_perdisk - a plugin for munin-node-win32-cygwin
#  *
#  * Copyright (c) 2015 John Vinopal
#  * Licensed under the MIT license.
#  */
use strict;
use warnings;
use Win32Helper;

#
# Munin plugin to monitor individual disk statistics.
#
# Parameters:
#	config
#	autoconf
#
# Requirements:
#	Cygwin, perl, and perl-libwin32 package or equivalent
#

# Performance Data from Win32 performance counter class.
# https://msdn.microsoft.com/en-us/library/aa394261(v=vs.85).aspx
my $win32Namespace = 'winmgmts://./root/cimv2';
my $win32Class     = 'Win32_PerfFormattedData_PerfDisk_LogicalDisk';
my @win32Props =
  qw(PercentDiskTime PercentIdleTime PercentDiskReadTime PercentDiskWriteTime);

# Munin Configuration
my $values = fetchDiskValues();
if (@ARGV) {
    if ( $ARGV[0] eq 'autoconf' ) {
        print "yes\n";
        exit 0;
    }
    elsif ( $ARGV[0] eq 'config' ) {
        print "graph_category disk\n";
        print "graph_title Disk usage in percent\n";
        print "graph_args --lower-limit 0 --upper-limit 100\n";
        print "graph_vlabel %\n";
        print "graph_scale no\n";
        print "graph_info This graph shows disk usage on the machine.\n";

        foreach my $disk ( sort keys %$values ) {
            next unless ( $disk =~ /^(\w):$/ );    # "C:"
            print "$1.label $disk\n";
        }
        exit 0;
    }
    else {
        print "Unknown arg $ARGV[0]\n";
        exit 1;
    }
}

# Munin Data

foreach my $disk ( sort keys %$values ) {
    next unless ( $disk =~ /^(\w):$/ );    # "C:"
    printf "%s.value %.2f\n", $1, $values->{$disk}->{PercentDiskTime};
}
exit 0;

# Return disk performance counters.
sub fetchDiskValues {
    return getWin32Props( $win32Namespace, $win32Class, @win32Props );
}
